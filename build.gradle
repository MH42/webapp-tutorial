buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
    }
}

plugins {
    id "com.moowork.node" version "0.13"
}

node {
    version = '6.7.0'
    npmVersion = '3.10.8'
    download = true
    workDir = file("${rootProject.buildDir}/node")
    nodeModulesDir = file("${project.projectDir}")
}

npm_install.inputs.file new File(projectDir, "package.json")
npm_install.outputs.dir fileTree(dir: "node_modules", exclude: "node_modules/.bin/**")

npm_run_build.inputs.dir new File(projectDir, "src")
npm_run_build.outputs.dir new File(projectDir, "dist")

task prepareJS {}
prepareJS.dependsOn npm_install

allprojects {

    apply plugin: 'idea'
    apply plugin: 'eclipse'

    task wrapper(type: Wrapper) {
        gradleVersion = '3.1'
    }

    repositories {
        mavenCentral()
    }

    task cleanJS << {
        delete 'node_modules'
        delete 'typings'
        delete 'bower_components'
    }

    idea {
        module {
            excludeDirs += file('node_modules')
            excludeDirs += file('bower_components')
            excludeDirs += file('dist')
        }
    }
}

